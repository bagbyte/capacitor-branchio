buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resValue "bool", "branchio_test_mode", "false"
        resValue "bool", "branchio_track_referral_active", "false"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':capacitor-android')

    implementation 'com.android.support:appcompat-v7:28.0.0'
    // required
    implementation 'io.branch.sdk.android:library:3.+'

    // optional
    implementation 'com.android.support:customtabs:28.0.0' // Chrome Tab matching
    implementation 'com.google.android.gms:play-services-ads:9+' // GAID matching
    implementation 'com.google.android.gms:play-services-appindexing:9.+' // App indexing

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

task configure() {
    println "Configuring the plugin"

    if (rootProject == project) {
        android.defaultConfig.resValue("string", "branchio_key_live", "")
        android.defaultConfig.resValue("string", "branchio_key_test", "")
        return
    }

    def jsonFile = file('../../../../capacitor.config.json')
    def config = new groovy.json.JsonSlurper().parseText(jsonFile.text)

    if (config.plugins && config.plugins.BranchIO) {
        if (!('keys' in config.plugins.BranchIO) || !('live' in config.plugins.BranchIO.keys) || !('test' in config.plugins.BranchIO.keys)) {
            throw new Exception("BranchIO plugin - Missing 'keys' in your 'capacitor.config.json'")
        }

        if ('test' in config.plugins.BranchIO) {
            android.defaultConfig.resValue("bool", "branchio_test_mode", String.valueOf(config.plugins.BranchIO.test))
        }

        if ('track_referral' in config.plugins.BranchIO) {
            android.defaultConfig.resValue("bool", "branchio_track_referral_active", String.valueOf(config.plugins.BranchIO.track_referral))
        }

        android.defaultConfig.resValue("string", "branchio_key_live", config.plugins.BranchIO.keys.live)
        android.defaultConfig.resValue("string", "branchio_key_test", config.plugins.BranchIO.keys.test)
    } else {
        throw new Exception("BranchIO plugin needs to be configured in your 'capacitor.config.json'")
    }
}
